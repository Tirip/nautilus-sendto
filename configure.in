AC_PREREQ(2.52)

AC_INIT(nautilus-sendto, 0.7, http://www.gnome.org)
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_MAINTAINER_MODE
                                                                                
AM_DISABLE_STATIC
AC_PROG_CC
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET


dnl --------------------------------------------------

GLIB_REQUIRED=2.4.0
GTK_REQUIRED=2.4.0
GLADE_REQUIRED=2.5.1
EBOOK_REQUIRED=1.5.3
BONOBO_ACTIVATION_REQUIRED=2.13.0
BONOBO_REQUIRED=2.13.0
GNOME_REQUIRED=2.13.0
GAIM_REQUIRED=1.5.0
NAUTILUS_EXTENSION=2.13.3
BLUETOOTH_REQUIRED=0.6.0
DBUS_REQUIRED=0.60

AC_SUBST(GLIB_REQUIRED)
AC_SUBST(GTK_REQUIRED)
AC_SUBST(GLADE_REQUIRED)
AC_SUBST(GNOME_REQUIRED)
AC_SUBST(EBOOK_REQUIRED)
AC_SUBST(GAIM_REQUIRED)
AC_SUBST(BONOBO_ACTIVATION_REQUIRED)
AC_SUBST(BONOBO_REQUIRED)
AC_SUBST(NAUTILUS_EXTENSION)
AC_SUBST(BLUETOOTH_REQUIRED)
AC_SUBST(DBUS_REQUIRED)

PKG_CHECK_MODULES(NAUTILUS_SENDTO,\
	 glib-2.0 >= $GLIB_REQUIRED                \
	 gthread-2.0 >= $GLIB_REQUIRED             \
	 gmodule-2.0 >= $GLIB_REQUIRED		  \
	 gtk+-2.0    >= $GTK_REQUIRED             \
	 libglade-2.0 >= $GLADE_REQUIRED            \
	 libbonobo-2.0 >= $BONOBO_REQUIRED	  \
	 libgnome-2.0 >= $GNOME_REQUIRED	\
	 libgnomeui-2.0 >= $GNOME_REQUIRED	\
	 bonobo-activation-2.0 >= $BONOBO_ACTIVATION_REQUIRED \
	 libnautilus-extension >= $NAUTILUS_EXTENSION \
	 gconf-2.0 >= $GNOME_REQUIRED)
AC_SUBST(NAUTILUS_SENDTO_CFLAGS)
AC_SUBST(NAUTILUS_SENDTO_LIBS)

dnl Libebook support
dnl ---------------------------------

PKG_CHECK_MODULES(NST_EBOOK, libebook-1.2 >= $EBOOK_REQUIRED,
	[
	AC_SUBST(NST_EBOOK_CFLAGS)
	AC_SUBST(NST_EBOOK_LIBS)
	echo "Using libebook-1.2 !"
	enable_evolution=yes
	],
	[
        echo "Building without evolution"
	enable_evolution=no
	]
)

AM_CONDITIONAL(HAVE_EVOLUTION, test "x$enable_evolution" = "xyes")

dnl Gaim support if it's installed
dnl ---------------------------------


PKG_CHECK_MODULES(GAIM, gaim >= $GAIM_REQUIRED,
	[
	AC_SUBST(GAIM_CFLAGS)
	AC_SUBST(GAIM_LIBS)
	GAIM_LIBDIR=`pkg-config --variable=libdir gaim`
	GAIM_DATADIR=`pkg-config --variable=datadir gaim`
	AC_SUBST(GAIM_LIBDIR)
	AC_SUBST(GAIM_DATADIR)
	enable_gaim=yes
	],
	[
	echo "Building without gaim"
	enable_gaim=no
	]
)

AM_CONDITIONAL(HAVE_GAIM, test "x$enable_gaim" = "xyes")

dnl Bluetooth support if it's installed
dnl -----------------------------------

PKG_CHECK_MODULES(BLUETOOTH, gnome-bluetooth >= $BLUETOOTH_REQUIRED,
	[
	AC_SUBST(BLUETOOTH_CFLAGS)
	AC_SUBST(BLUETOOTH_LIBS)
	enable_bluetooth=yes
	],
	[
	echo "Building without Bluetooth"
	enable_bluetooth=no
	]
)

AM_CONDITIONAL(HAVE_BLUETOOTH, test "x$enable_bluetooth" = "xyes")

dnl --------------------------------------------------

dnl Balsa support
dnl -------------------------------------------------

enable_balsa=no
AM_CONDITIONAL(HAVE_BALSA, test "x$enable_balsa" = "xyes")
dnl -------------------------------------------------


dnl Sylpheed support
dnl -------------------------------------------------

enable_sylpheed=yes
AM_CONDITIONAL(HAVE_SYLPHEED, test "x$enable_sylpheed" = "xyes")
dnl -------------------------------------------------


dnl Thunderbird support
dnl -------------------------------------------------

enable_thunderbird=no
AM_CONDITIONAL(HAVE_THUNDERBIRD, test "x$enable_thunderbird" = "xyes")
dnl -------------------------------------------------

dnl Gajim support
dnl -------------------------------------------------

AC_ARG_WITH(gajim,
        [AS_HELP_STRING(--with-gajim,[Path to Gajim install prefix])],
        [GAJIM_PATH="$withval"],[GAJIM_PATH="$prefix"])

GAJIM_SHARE_DIR="$GAJIM_PATH"/share/gajim

PKG_CHECK_MODULES(DBUS, dbus-1 >= $DBUS_REQUIRED dbus-glib-1 >= $DBUS_REQUIRED,
        [
        AC_SUBST(DBUS_CFLAGS)
        AC_SUBST(DBUS_LIBS)
	if test -f "$GAJIM_SHARE_DIR"/data/pixmaps/gajim.png ;
	then
		enable_gajim=yes
		AC_DEFINE_UNQUOTED(GAJIM_SHARE_DIR, "$GAJIM_SHARE_DIR", [path to gajim share dir])
	else
		echo "Building without Gajim support"
		enable_gajim=no
	fi
        ],
        [
        echo "Building without Gajim support"
        enable_gajim=no
        ]
)

AM_CONDITIONAL(HAVE_GAJIM, test "x$enable_gajim" = "xyes")

dnl -------------------------------------------------


GNOME_COMPILE_WARNINGS

GNOME_COMMON_INIT

ALL_LINGUAS="bg ca cs da de el en_CA en_GB es eu fi fr gl he hu it ja ko lt mk nb ne nl or pa pl pt pt_BR ru rw sk sq sv th vi uk zh_CN zh_TW"

GETTEXT_PACKAGE=nautilus-sendto
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [GetText Package])
                                                                               
AM_GLIB_GNU_GETTEXT
AC_PROG_INTLTOOL

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(LOCALEDIR, "${ac_default_prefix}/${DATADIRNAME}/locale", [Locale dir])
else
  AC_DEFINE_UNQUOTED(LOCALEDIR, "${prefix}/${DATADIRNAME}/locale", [Locale dir])
fi

AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2

AC_OUTPUT([
Makefile
src/Makefile
src/plugins/Makefile
gaim_plugin/Makefile
po/Makefile.in
])

echo "-------------------------------------------------------"
echo "                                                       "

if test "x${enable_evolution}" = "xyes" ; then
   echo "     Enable evolution plugin : YES"
else
   echo "     Enable evolution plugin : NO"
fi

if test "x${enable_gaim}" = "xyes" ; then
   echo "     Enable gaim plugin : YES"
else
   echo "     Enable gaim plugin : NO"
fi

if test "x${enable_bluetooth}" = "xyes" ; then
   echo "     Enable bluetooth plugin : YES"
else
   echo "     Enable bluetooth plugin : NO"
fi

if test "x${enable_balsa}" = "xyes" ; then
   echo "     Enable balsa plugin : YES"
else
   echo "     Enable balsa plugin (under development, don't support attachments) : NO"
fi

if test "x${enable_sylpheed}" = "xyes" ; then
   echo "     Enable sylpheed plugin : YES"
else
   echo "     Enable sylpheed plugin : NO"
fi

if test "x${enable_thunderbird}" = "xyes" ; then
   echo "     Enable thunderbird plugin : YES"
else
   echo "     Enable thunderbird plugin (under development, don't support attachments) : NO"
fi

if test "x${enable_gajim}" = "xyes" ; then
   echo "     Enable gajim plugin : YES"
else
   echo "     Enable gajim plugin : NO"
fi

echo "                                                       "
echo "-------------------------------------------------------"
